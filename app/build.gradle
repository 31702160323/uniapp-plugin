import groovy.xml.XmlParser
import org.json.JSONException
import org.json.JSONObject

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33
//    buildToolsVersion '30.0.3'

    def xml = new XmlParser().parse("${projectDir}/src/main/assets/data/dcloud_control.xml")
    def appid = xml.apps.app[0].attributes().get("appid")
    def manifest = new JSONObject(new File("${projectDir}/src/main/assets/apps/${appid}/www/manifest.json").getText('UTF-8'))

    defaultConfig {
        applicationId "com.xzh.UniPlugin"
        minSdk 21
        targetSdk 33 //建议此属性值设为21 io.dcloud.PandoraEntry 作为apk入口时   必须设置 targetSDKVersion>=21 沉浸式才生效
        versionCode = Integer.parseInt(manifest.get("version").get("code"))
        versionName = manifest.get("version").get("name")

        ndk {
            abiFilters 'x86'
            abiFilters 'x86_64'
            abiFilters 'arm64-v8a'
//            abiFilters 'armeabi-v7a'
        }
        resConfigs("zh-rCN")
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }
        kotlinOptions {
            jvmTarget = '11'
        }
    }

    signingConfigs {
        config {
            keyAlias 'uniplugin'
            keyPassword '185007'
            storeFile file('uniplugin.keystore')
            storePassword '185007'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.config
            shrinkResources true
            zipAlignEnabled true
            minifyEnabled true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    DCLOUD_APPKEY: "3962ee207b721a20e35bb6ae110fed34", // 27:19:6E:38:6B:87:5E:76:AD:F7:00:E7:EA:84:E4:C6:EE:E3:3D:FA
            ]
        }
        debug {
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    DCLOUD_APPKEY: "3962ee207b721a20e35bb6ae110fed34", // 27:19:6E:38:6B:87:5E:76:AD:F7:00:E7:EA:84:E4:C6:EE:E3:3D:FA
            ]
        }
    }
    //使用uniapp时，需复制下面代码
    /*代码开始*/
    aaptOptions {
        additionalParameters '--auto-add-overlay'
        //noCompress 'foo', 'bar'
        ignoreAssetsPattern "!.svn:!.git:.*:!CVS:!thumbs.db:!picasa.ini:!*.scc:*~"
    }
    /*代码结束*/

    //导入aar需要的配置
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name.equals('debug')) {
                outputFileName  = "android_debug.apk"
            } else {
                outputFileName  = "XZH_" + defaultConfig.versionName + "_" + defaultConfig.versionCode + "_" + new Date().format("yyyy-MM-dd") + "_" + buildType.name + ".apk"
            }
//            output.processManifestProvider.get().doLast {}
        }

        variant.mergeAssetsProvider.get().doLast {
            def isDebug = variant.buildType.name == 'debug'
            System.out.println("环境：" + variant.buildType.name)
            System.out.println("versionCode：" + defaultConfig.versionCode)
            System.out.println("versionName：" + defaultConfig.versionName)
            def includes = ["uni-jsframework${isDebug ? "" : "-dev"}.js", "uni-jsframework-vue3${isDebug ? "" : "-dev"}.js"]
            def vueVersion = "2"

            try {
                vueVersion = manifest.get("plus").get("uni-app").get("vueVersion")
            } catch (IOException | JSONException e) {
                vueVersion = "2"
            }

            xml.attributes().put("debug", isDebug)
            xml.attributes().put("syncDebug", isDebug)

            System.out.println("vue：" + vueVersion)
            if (vueVersion == "3") {
                includes.add("uni-jsframework${!isDebug ? "" : "-dev"}.js")
            } else {
                includes.add("uni-jsframework-vue3${!isDebug ? "" : "-dev"}.js")
            }

            //删除assets文件夹下的所有zip文件，或者指定文件。(删除文件夹：文件夹名/**/*)
//            delete(fileTree(dir: variant.mergeAssetsProvider.get().outputDir, includes: includes))

            def findTargetFile = false
            outputs.files.getFiles().find {
                it.listFiles().find {
                    if (it.toString().contains("data")) {
                        it.listFiles().find {
                            findTargetFile = it.toString().contains("dcloud_control.xml")
                            if (findTargetFile) {
                                new File(it.toString()).write(groovy.xml.XmlUtil.serialize(xml), 'UTF-8')
                            }
                            return findTargetFile
                        }
                    }
                    return findTargetFile
                }
                return findTargetFile
            }
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.?ar'], dir: 'libs')

    debugImplementation fileTree(dir: 'libs', include: ['debug-server-release.aar'])
    debugImplementation 'com.squareup.okhttp3:okhttp:3.12.12'
    debugImplementation 'com.squareup.okio:okio:1.15.0'

    implementation 'androidx.core:core-ktx:1.8.0'
//    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'

    /*uniapp所需库-----------------------开始*/
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.facebook.fresco:fresco:1.13.0'
    implementation "com.facebook.fresco:animated-gif:1.13.0"
    /*uniapp所需库-----------------------结束*/
    // 基座需要，必须添加
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.alibaba:fastjson:1.2.83'
    implementation 'androidx.webkit:webkit:1.3.0'

    // 添加uni-app插件
    implementation project(':grayview')
    implementation project(':floatview')
//    implementation project(':autostart')
//    implementation project(':calendar')
    implementation project(":musicNotification")
    implementation project(":widget")
//    implementation(":varyDesktopIcons")
//    implementation(":pictureSelectorUni")
    implementation project(":shortcuts")
}
